10 REM ***** [Hit That Letter]
20 REM PROGRAM BY Shameem Ejaz
30 REM ***********************
40 KEY OFF
50 COLOR 15,4,14
60 H=0:U=0:S=0:Z=0:X=0:N$="":K$=""
70 CLS
80 LOCATE 2,33:PRINT "Hit That Letter"
90 REM ***** [DRAWING WINDOW]
100 LOCATE 3,20:PRINT CHR$(218)
110 LOCATE 16,20:PRINT CHR$(192)
120 FOR X = 21 TO 60
130 LOCATE 3,X:PRINT CHR$(196)
140 LOCATE 16,X:PRINT CHR$(196)
150 NEXT X
160 LOCATE 3,61:PRINT CHR$(191)
170 LOCATE 16,61:PRINT CHR$(217)
180 FOR X = 4 TO 15
190 LOCATE X,20:PRINT CHR$(179)
200 LOCATE X,61:PRINT CHR$(179)
210 NEXT X
220 REM ***** [WINDOW DRWAING ENDS]
230 REM ***** [DISPLAYING INSTRUCTIONS and else]
240 COLOR 14,4,14
250 LOCATE 18,5:PRINT "INSTRUCTIONS: "
260 COLOR 15,4,14
270 LOCATE 18,19:PRINT "In the window above, you will see different letters popup."
280 LOCATE 19,19:PRINT "You have to hit corresponding key on keyboard. Keep CapsLock"
290 LOCATE 20,19:PRINT "on during play."
300 REM ***** [instructions for speed within window]
310 COLOR 14,4,14
320 LOCATE 22,28:PRINT "A game by Shameem Ejaz"
330 COLOR 0,4,14
340 LOCATE 23,28:PRINT STRING$(22," ")
350 LOCATE 4,63:PRINT "Date: ";DATE$
360 COLOR 14,4,14
370 LOCATE 5,25:PRINT "You can select game speed here"
380 LOCATE 6,25:PRINT "by pressing 1,2,3 and enter OR"
390 LOCATE 7,25:PRINT "4 to end the game"
400 LOCATE 9,30:PRINT "1 - Very slow"
410 LOCATE 10,30:PRINT "2 - Slow"
420 LOCATE 11,30:PRINT "3 - Fast"
430 LOCATE 12,30:PRINT "4 - Quit game"
440 LOCATE 14,25:INPUT "Your choice";A
450 COLOR 15,4,14
460 REM ***** [checking speed input]
470 IF A=1 THEN 510 ELSE 480
480 IF A=2 THEN 520 ELSE 490
490 IF A=3 THEN 530 ELSE 500
500 IF A=4 THEN 830 ELSE 50
510 S=2000:GOTO 550
520 S=800:GOTO 550
530 S=150:GOTO 550
540 REM ***** [clearing window to blue]
550 COLOR 15,1,14
560 FOR X = 4 TO 15
570 LOCATE X,21:PRINT STRING$(40," ")
580 NEXT X
590 REM ***** [randomly poping letters]
600 RANDOMIZE 26
610 LOCATE 4,4:PRINT " Hit(s):"
620 FOR U = 1 TO 26
630 X = INT(65+26*RND)
640 R = INT(4+11*RND)
650 C = INT(21+40*RND)
660 REM ***** [LETTER DISPLAY AND RECODING KEY PRESS]
670 FOR Z = 1 TO S
680 LOCATE R,C:PRINT CHR$(X)
690 K$=INKEY$
700 IF K$=CHR$(X) THEN H=H+1:SOUND 890,9
710 LOCATE 4,12:PRINT H
720 NEXT Z
730 LOCATE R,C:PRINT " "
740 NEXT U
750 COLOR 15,1,14
760 REM ***** [DISPLAYING SCORE AND %AGE ON END]
770 LOCATE 6,25:PRINT "Your final score is";H
780 LOCATE 7,25:PRINT "with";INT((H/26)*100);"percent hits"
790 COLOR 15,4,14
800 LOCATE 24,28:INPUT "Play again [y/n]";N$
810 IF N$="Y" OR N$="y" THEN 50 ELSE 840
820 COLOR 15,4,14
830 REM ***** [program end routine]
840 COLOR 7,0,0
850 CLS
860 LOCATE 13,28:PRINT "Thankyou for playing HTL"
870 PRINT :PRINT :PRINT :PRINT :PRINT :PRINT :PRINT :PRINT :PRINT
880 END
